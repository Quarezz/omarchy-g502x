import QtQuick
import Quickshell
import qs.Commons
import qs.Ui
import "Model.js" as Model

Panel {
  id: root
  moduleName: "morf.g502x"
  manageIpc: false

  property var anchorItem: null
  property var hostWidget: null
  readonly property var barIdentity: hostWidget || root

  readonly property color foreground: bar ? bar.foreground : Color.foreground
  readonly property color urgent: bar ? bar.urgent : Color.urgent
  readonly property string fontFamily: bar ? bar.fontFamily : Style.font.family
  readonly property color dim: Qt.darker(foreground, 1.4)

  readonly property string deviceTitle: (hostWidget && hostWidget.deviceTitle) ? hostWidget.deviceTitle : "G502 X"
  readonly property string mouseIcon: (hostWidget && hostWidget.mouseIcon) ? hostWidget.mouseIcon : Model.mouseIcon()
  readonly property int batteryPercent: hostWidget && hostWidget.batteryPercent !== undefined ? hostWidget.batteryPercent : -1
  readonly property real batteryFraction: hostWidget && hostWidget.batteryFraction !== undefined ? hostWidget.batteryFraction : 0
  readonly property bool charging: !!(hostWidget && hostWidget.charging)
  readonly property bool discharging: !!(hostWidget && hostWidget.discharging)
  readonly property bool fullyCharged: !!(hostWidget && hostWidget.fullyCharged)
  readonly property bool lowBattery: !!(hostWidget && hostWidget.lowBattery)
  readonly property string modeLabel: (hostWidget && hostWidget.modeLabel) ? hostWidget.modeLabel : ""
  readonly property int dpi: hostWidget && hostWidget.dpi !== undefined ? hostWidget.dpi : -1
  readonly property var dpiPresets: (hostWidget && hostWidget.dpiPresets) ? hostWidget.dpiPresets : Model.dpiPresets()
  readonly property bool dpiBusy: !!(hostWidget && hostWidget.dpiBusy)
  readonly property color fillColor: (hostWidget && hostWidget.batteryColor) ? hostWidget.batteryColor : (lowBattery ? urgent : foreground)
  property int dpiCursor: -1

  readonly property string heroStatusText: modeLabel || "Standing by"

  function setDpi(value) {
    if (hostWidget && typeof hostWidget.setDpi === "function") hostWidget.setDpi(value)
  }

  function switchPanel(direction) {
    if (bar && typeof bar.switchPanelFrom === "function")
      return bar.switchPanelFrom(barIdentity, direction)
    return false
  }

  onOpenedChanged: if (opened) {
    dpiCursor = -1
    if (hostWidget && typeof hostWidget.refresh === "function") hostWidget.refresh()
  }

  KeyboardPanel {
    id: panel
    anchorItem: root.anchorItem
    owner: root.barIdentity
    bar: root.bar
    open: root.opened
    focusTarget: keyCatcher
    contentWidth: panel.fittedContentWidth(Style.space(380))
    contentHeight: panel.fittedContentHeight(column.implicitHeight)

    PanelKeyCatcher {
      id: keyCatcher
      anchors.fill: parent
      onCloseRequested: root.close()
      onTabRequested: function(direction) { root.switchPanel(direction) }
      onMoveRequested: function(dx, dy) {
        if (dx === 0) return
        var list = root.dpiPresets
        if (!list || list.length === 0) return
        var idx = root.dpiCursor
        if (idx < 0) {
          for (var i = 0; i < list.length; i++) {
            if (list[i] === root.dpi) { idx = i; break }
          }
          if (idx < 0) idx = 0
        }
        idx = Math.max(0, Math.min(list.length - 1, idx + dx))
        root.dpiCursor = idx
        root.setDpi(list[idx])
      }

      Column {
        id: column
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.top: parent.top
        spacing: Style.space(14)

        Item {
          width: parent.width
          implicitHeight: Math.max(heroIcon.implicitHeight, heroLabels.implicitHeight, heroPercent.implicitHeight)

          Text {
            id: heroIcon
            text: root.mouseIcon
            color: root.fillColor
            font.family: root.fontFamily
            font.pixelSize: Style.font.display
            anchors.left: parent.left
            anchors.verticalCenter: parent.verticalCenter

            Behavior on color { ColorAnimation { duration: 200 } }
          }

          Column {
            id: heroLabels
            anchors.left: heroIcon.right
            anchors.leftMargin: Style.space(14)
            anchors.right: heroPercent.left
            anchors.rightMargin: Style.space(10)
            anchors.verticalCenter: parent.verticalCenter
            spacing: Style.space(2)

            Text {
              text: root.deviceTitle
              color: root.foreground
              font.family: root.fontFamily
              font.pixelSize: Style.font.title
              font.bold: true
              elide: Text.ElideRight
              width: parent.width
            }

            Text {
              id: heroStatus
              text: root.heroStatusText.toUpperCase()
              color: root.dim
              font.family: root.fontFamily
              font.pixelSize: Style.font.caption
              font.bold: true
              font.letterSpacing: 1.2
              elide: Text.ElideRight
              width: parent.width
            }
          }

          Text {
            id: heroPercent
            text: root.batteryPercent >= 0 ? (root.batteryPercent + "%") : "—"
            color: root.fillColor
            font.family: root.fontFamily
            font.pixelSize: Style.font.displayLarge
            font.bold: true
            anchors.right: parent.right
            anchors.verticalCenter: parent.verticalCenter

            Behavior on color { ColorAnimation { duration: 200 } }
          }
        }

        Item {
          width: parent.width
          implicitHeight: Style.space(8)

          Rectangle {
            id: barTrack
            anchors.fill: parent
            radius: height / 2
            color: Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.12)
          }

          Rectangle {
            id: barFill
            anchors.left: barTrack.left
            anchors.verticalCenter: barTrack.verticalCenter
            height: barTrack.height
            radius: barTrack.radius
            color: root.fillColor
            width: Math.max(barTrack.height, barTrack.width * root.batteryFraction)

            Behavior on width { NumberAnimation { duration: 320; easing.type: Easing.OutCubic } }
            Behavior on color { ColorAnimation { duration: 220 } }
          }
        }

        Row {
          width: parent.width
          spacing: Style.space(20)

          Column {
            width: (parent.width - parent.spacing) / 2
            spacing: Style.spacing.labelGap
            InfoPair { label: "State"; value: root.modeLabel || "—" }
            InfoPair { label: "Connection"; value: "LIGHTSPEED" }
          }

          Column {
            width: (parent.width - parent.spacing) / 2
            spacing: Style.spacing.labelGap
            InfoPair { label: "Sensitivity"; value: Model.formatDpi(root.dpi) }
            InfoPair { label: "Charge"; value: Model.chargeLabel(root.lowBattery, root.fullyCharged, root.charging, root.discharging) }
          }
        }

        PanelSeparator {
          foreground: root.foreground
        }

        Column {
          width: parent.width
          spacing: Style.space(10)

          PanelSectionHeader {
            text: "SENSITIVITY"
            foreground: root.foreground
            fontFamily: root.fontFamily
          }

          Row {
            id: dpiRow
            width: parent.width
            spacing: Style.space(6)

            readonly property real cellWidth: root.dpiPresets.length > 0
              ? (width - spacing * (root.dpiPresets.length - 1)) / root.dpiPresets.length
              : 0

            Repeater {
              model: root.dpiPresets
              Button {
                required property var modelData
                required property int index
                width: dpiRow.cellWidth
                text: String(modelData)
                fontSize: Style.font.bodySmall
                foreground: root.foreground
                fontFamily: root.fontFamily
                horizontalPadding: Style.spacing.controlPaddingX
                verticalPadding: Style.spacing.controlPaddingY + Style.space(2)
                bordered: true
                enabled: !root.dpiBusy
                active: root.dpi === modelData
                hasCursor: root.dpiCursor === index
                onClicked: root.setDpi(modelData)
                onHovered: function(h) {
                  if (h) root.dpiCursor = index
                }
              }
            }
          }
        }
      }
    }
  }

  component InfoPair: Row {
    property string label: ""
    property string value: ""

    width: parent.width
    spacing: Style.space(8)

    InfoLabel { text: label }
    Item {
      width: Math.max(0, parent.width - parent.children[0].implicitWidth - parent.children[2].implicitWidth - parent.spacing * 2)
      height: 1
    }
    InfoValue { text: value }
  }

  component InfoLabel: Text {
    color: root.bar ? root.bar.foreground : root.foreground
    opacity: 0.6
    font.family: root.fontFamily
    font.pixelSize: Style.font.bodySmall
  }

  component InfoValue: Text {
    color: root.bar ? root.bar.foreground : root.foreground
    font.family: root.fontFamily
    font.pixelSize: Style.font.bodySmall
  }
}
